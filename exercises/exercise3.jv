pipeline economy {
  economyExtractor
      -> economyExcelInterpreter
      -> economySheetpicker
      -> renamecc -> renamegpc -> renamebis
      -> DataSelector
        -> bondIssuanceTableInterpreter -> bondIssuanceLoader;
      DataSelector 
        -> gdpPerCapitaTableInterpreter -> gdpPerCapita;
        

  block economyExtractor oftype HttpExtractor {
      url: "https://thedocs.worldbank.org/en/doc/7d852628d96b9411d43e5d36d5dff941-0050062022/original/Graphs-Chapter-5-02082022.xlsx";
  }

  block economyExcelInterpreter oftype XLSXInterpreter { }

  block economySheetpicker oftype SheetPicker {
  sheetName: 'Figure S5.1.2';
  }

  block renamecc oftype CellWriter {
    at: cell P2;
    write: ["Country Code"];
  }

  block renamegpc oftype CellWriter {
    at: cell R2;
    write: ["GDP per Capita"];
  }

  block renamebis oftype CellWriter {
    at: cell S2;
    write: ["Bond Issuance Share"];
  }

  block DataSelector oftype CellRangeSelector {
    select: range P2:S45;
  }

  block bondIssuanceTableInterpreter oftype TableInterpreter {
    header: true;
    columns: [
        "Country Code" oftype CountryCodeAlpha3,
        "Bond Issuance Share" oftype bond_decimal
        ];
  }

  block gdpPerCapitaTableInterpreter oftype TableInterpreter {
    header: true;
    columns: [
        "Country Code" oftype CountryCodeAlpha3,
        "GDP per Capita" oftype gdp_decimal,
        ];
  }


  block bondIssuanceLoader oftype SQLiteLoader {
    table: "bondIssuance";
    file: "./economy.sqlite";
  }

  block gdpPerCapita oftype SQLiteLoader {
    table: "gdpPerCapita";
    file: "./economy.sqlite";
  }

}

valuetype gdp_decimal oftype decimal {
  constraints: [
    Validgdp
  ];
}
constraint Validgdp on decimal: value >= 0;

valuetype bond_decimal oftype decimal {
  constraints: [
    Validbond
  ];
}
constraint Validbond on decimal: value >= 0 and value <= 1;